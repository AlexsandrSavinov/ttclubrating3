
Перем мНастройкиОтчета;

Функция СформироватьКарточкуУчастника(АдресДанных) Экспорт
	
	мНастройкиОтчета = ПолучитьИзВременногоХранилища(АдресДанных);
	
	ТекУчастник 	= мНастройкиОтчета.Участник;
	НачПериод 		= мНастройкиОтчета.НачалоПериода;
	КонПериод		= мНастройкиОтчета.КонецПериода;
	
	ТабДок = Новый ТабличныйДокумент;
	Макет = ЭтотОбъект.ПолучитьМакет("Макет");
	ЗаголовокОбласть = Макет.ПолучитьОбласть("Заголовок");
	
	ЗаголовокОбласть.Параметры.ОтчетНаДату	= "Данные на: "+Формат(ТекущаяДата(),"ДФ=dd.MM.yyyy");
	ЗаголовокОбласть.Параметры.ФИО 					= ТекУчастник.Наименование;
	ЗаголовокОбласть.Параметры.ДатаРождения	= Формат(ТекУчастник.ДатаРождения,"ДЛФ=DD");		
	ЗаголовокОбласть.Параметры.ТекРейтинг		=	Строка(Серверные.ПолучитьТекущийРейтингУчастника(ТекУчастник,ТекущаяДата(),мНастройкиОтчета.ВидРейтинга))+
	?(мНастройкиОтчета.ВидРейтинга = Справочники.ВидыРейтинга.ПустаяСсылка(),""," ("+Строка(НРег(мНастройкиОтчета.ВидРейтинга))+")");
	
	ТекРазряд = Серверные.ПолучитьРазрядЗваниеУчастника(ТекУчастник,ТекущаяДата());
	ЗаголовокОбласть.Параметры.ТекРазряд 		= ?(ТекРазряд = "Присвоить разряд","",ТекРазряд); 
	ЗаголовокОбласть.Параметры.Тренер				= ТекУчастник.ТренерыСписком;
	ЗаголовокОбласть.Параметры.Клуб					= ТекУчастник.Клуб;	
	ЗаголовокОбласть.Параметры.Адрес					= ""+ТекУчастник.Город+","+ТекУчастник.ФактическийАдрес;
	ЗаголовокОбласть.Рисунки.ФотоУчастника.Картинка	= Новый Картинка(ТекУчастник. ФайлКартинки.Получить());
	ТабДок.Вывести(ЗаголовокОбласть);
	
	ОбластьСтатистики = Макет.ПолучитьОбласть("ОбластьСтатистики");
	
	ТабДок.НачатьГруппуСтрок();
	ДанныеДиаграммы = ПолучитьДанныеДляДиаграммы(ТекУчастник,НачПериод,КонПериод,мНастройкиОтчета.ВидРейтинга);
	ДиаграммаДляЗаполнения = ОбластьСтатистики.Рисунки.СтатистикаДиаграмма.Объект;
	ДиаграммаДляЗаполнения.Обновление = Ложь;
	ДиаграммаДляЗаполнения.Очистить();
	ДиаграммаДляЗаполнения.РежимСглаживания = РежимСглаживанияДиаграммы.ГладкаяКривая;
	ДиаграммаДляЗаполнения.Анимация = АнимацияДиаграммы.Использовать;
	ДиаграммаДляЗаполнения.ВидПодписей = ВидПодписейКДиаграмме.Нет;
	ТочкиТЗ = ДанныеДиаграммы.Скопировать(,"ДатаДок"); //даты рейтинга
	ТочкиТЗ.Свернуть("ДатаДок");
	СерииТЗ = ДанныеДиаграммы.Скопировать(,"ВидРейтинга"); //виды рейтинга
	СерииТЗ.Свернуть("ВидРейтинга");
		
	Для Каждого ТекСерия Из СерииТЗ Цикл
		ТекущаяСерия = ДиаграммаДляЗаполнения.УстановитьСерию(ТекСерия.ВидРейтинга);
		Если ТекСерия.ВидРейтинга = Справочники.ВидыРейтинга.ПустаяСсылка() Тогда
			ТекущаяСерия.Текст = "Нет вида рейтинга";
		Иначе
			ТекущаяСерия.Текст = Строка(ТекСерия.ВидРейтинга);
		КонецЕсли;
		//ищем для каждой серии точки
		СтрокиТочек = ДанныеДиаграммы.НайтиСтроки(Новый Структура("ВидРейтинга",ТекСерия.ВидРейтинга));
		Для Каждого ТекТочка Из СтрокиТочек Цикл
			ТекущаяТочка = ДиаграммаДляЗаполнения.УстановитьТочку(ТекТочка.ДатаДок);
			ТекущаяТочка.Текст = Формат(ТекТочка.ДатаДок,"ДФ=d.M.yy");
			Подсказка = ?(ТипЗнч(ТекТочка.Регистратор) = Тип("ДокументСсылка.ПроведениеСоревнования"),ТекТочка.Регистратор.НазваниеСоревнования,"") + " "+ ТекТочка.Рейтинг;
			ДиаграммаДляЗаполнения.УстановитьЗначение(ТекущаяТочка,ТекущаяСерия,ТекТочка.Рейтинг,ТекТочка.Регистратор,Подсказка);
		КонецЦикла;
	КонецЦикла;
	ДиаграммаДляЗаполнения.ОбластьЗаголовка.Текст = "Динамика изменения рейтинга за "+ПредставлениеПериода(НачПериод,КонПериод,"ФП = Истина");
	ДиаграммаДляЗаполнения.Обновление	= Истина;
	ТабДок.Вывести(ОбластьСтатистики);
	ТабДок.ЗакончитьГруппуСтрок();
	
	//теперь заполняем список соревнований в которых участник участвовал
	ОбластьШапкаСоревнований = Макет.ПолучитьОбласть("ШапкаСпискаСоревнований");
	//Принимал участие в соревнованиях:
	Если ТекУчастник.Пол = Перечисления.ПолУчастника.Женский Тогда
		ДобавитьА = "а";
	Иначе
		ДобавитьА = "";
	КонецЕсли;
	
	Если ДанныеДиаграммы.Количество() = 0 Тогда
		ОбластьШапкаСоревнований.Параметры.ЗаголовокДляСписка = "Пока еще не участвовал"+ДобавитьА+" ни в одном соревновании";
	Иначе
		ОбластьШапкаСоревнований.Параметры.ЗаголовокДляСписка = "Принимал"+ДобавитьА+" участие в "+ДанныеДиаграммы.Количество()+?(ДанныеДиаграммы.Количество() = 1," соревновании:"," соревнованиях:");
	КонецЕсли;
	ТабДок.Вывести(ОбластьШапкаСоревнований);
	ТабДок.НачатьГруппуСтрок();
	
	ОбластьСтрокиСоревнований = Макет.ПолучитьОбласть("СтрокаСписка");
	Сч = 1;
	ИтогПоПриросту = 0;
	Для Каждого ТекСоревнование Из ДанныеДиаграммы Цикл
		 ОбластьСтрокиСоревнований.Параметры.Сч = ""+Сч+".";
		 Сч = Сч +1;
		 //сформируем текст для соревнования
		 ТекстСоревнования = "";
		 Если ЗначениеЗаполнено(ТекСоревнование.Регистратор.НазваниеСоревнования) Тогда
			 ТекстСоревнования = ТекСоревнование.Регистратор.НазваниеСоревнования +" от "+Формат(ТекСоревнование.Регистратор.Дата,"ДЛФ=DD");
		 Иначе
			 ТекстСоревнования = Строка(ТекСоревнование.Регистратор.НазваниеСоревнования);
		 КонецЕсли;
		 ОбластьСтрокиСоревнований.Параметры.Соревнование = ТекстСоревнования;
		 ОбластьСтрокиСоревнований.Параметры.СсылкаРасшифровки = ТекСоревнование.Регистратор;
		 ОбластьСтрокиСоревнований.Параметры.СтарР 			= ТекСоревнование.СтарыйРейтинг;
		 ОбластьСтрокиСоревнований.Параметры.Прирост 		= ТекСоревнование.Прирост;
		 ОбластьСтрокиСоревнований.Параметры.НовРейтинг	 = ТекСоревнование.Рейтинг;
		 ТабДок.Вывести(ОбластьСтрокиСоревнований);
		 //ИтогПоПриросту = ИтогПоПриросту + ТекСоревнование.Прирост; 
	КонецЦикла;
	//ОбластьИтогов = Макет.ПолучитьОбласть("ИтогТаблицы");
	//ОбластьИтогов.Параметры.ИтогПрироста = ИтогПоПриросту;
	//ТабДок.Вывести(ОбластьИтогов);
	ТабДок.ЗакончитьГруппуСтрок();
	Возврат ТабДок;
	
КонецФункции

Функция ПолучитьДанныеДляДиаграммы(ТекУчастник,НачПериод,КонПериод,перемВидРейтинга)
	
	Запрос = Новый Запрос;
	Текст = "ВЫБРАТЬ
	|	РейтингУчастников.Регистратор
	|ПОМЕСТИТЬ втТаблицаСоревнований
	|ИЗ
	|	РегистрСведений.РейтингУчастников КАК РейтингУчастников
	|ГДЕ
	|	РейтингУчастников.Период МЕЖДУ &Нач И &Кон
	|	И РейтингУчастников.Участник = &Участник
	|	И РейтингУчастников.Регистратор ССЫЛКА Документ.ПроведениеСоревнования
	|
	|СГРУППИРОВАТЬ ПО
	|	РейтингУчастников.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РейтингУчастников.Участник,
	|	РейтингУчастников.Участник.ДатаРождения КАК ДатаРождения
	|ПОМЕСТИТЬ втВсеУчастники
	|ИЗ
	|	втТаблицаСоревнований КАК втТаблицаСоревнований
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РейтингУчастников КАК РейтингУчастников
	|		ПО втТаблицаСоревнований.Регистратор = РейтингУчастников.Регистратор
	|ГДЕ
	|	РейтингУчастников.Участник = &Участник
	|
	|СГРУППИРОВАТЬ ПО
	|	РейтингУчастников.Участник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТаблицаСоревнований.Регистратор,
	|	втВсеУчастники.Участник
	|ПОМЕСТИТЬ втПолнаяТаблица
	|ИЗ
	|	втТаблицаСоревнований КАК втТаблицаСоревнований,
	|	втВсеУчастники КАК втВсеУчастники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПолнаяТаблица.Регистратор.Дата КАК ДатаДок,
	|	втПолнаяТаблица.Участник КАК Участник,
	|	РейтингУчастников.Рейтинг,
	|	втПолнаяТаблица.Регистратор,
	|	ВЫБОР
	|		КОГДА РейтингУчастников.ВидРейтинга ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыРейтинга.ПустаяСсылка)
	|		ИНАЧЕ РейтингУчастников.ВидРейтинга
	|	КОНЕЦ КАК ВидРейтинга,
	|	РейтингУчастников.Прирост,
	|	РейтингУчастников.СтарыйРейтинг
	|ИЗ
	|	втПолнаяТаблица КАК втПолнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РейтингУчастников КАК РейтингУчастников
	|		ПО втПолнаяТаблица.Регистратор = РейтингУчастников.Регистратор
	|			И втПолнаяТаблица.Участник = РейтингУчастников.Участник
	|ГДЕ
	|	ВЫБОР
	|			КОГДА РейтингУчастников.ВидРейтинга ЕСТЬ NULL 
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыРейтинга.ПустаяСсылка)
	|			ИНАЧЕ РейтингУчастников.ВидРейтинга
	|		КОНЕЦ = &ВидРейтинга
	|
	|УПОРЯДОЧИТЬ ПО
	|	втПолнаяТаблица.Регистратор.Дата,
	|	Участник";
	Запрос.Текст = Текст;
	Запрос.УстановитьПараметр("Участник",ТекУчастник);
	Запрос.УстановитьПараметр("Нач",НачПериод);
	Запрос.УстановитьПараметр("Кон",КонПериод);
	Запрос.УстановитьПараметр("ВидРейтинга",перемВидРейтинга);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Для Каждого ТекСтрока Из РезультатЗапроса Цикл
		Если ТекСтрока.Рейтинг = Null Тогда
			ТекСтрока.Рейтинг = Серверные.ПолучитьТекущийРейтингУчастника(ТекСтрока.Участник,ТекСтрока.ДатаДок,ТекСтрока.ВидРейтинга);
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатЗапроса;
	
КонецФункции

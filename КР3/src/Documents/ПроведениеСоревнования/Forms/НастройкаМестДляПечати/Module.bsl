
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РежимТура 		= Параметры.РежимТура;
	ТаблицаГрупп 	= Параметры.МассивГрупп;
	СеткаНа			= Параметры.СеткаНа;
	Элементы.ТаблицаМестНомерГруппы.Видимость = РежимТура = Перечисления.РежимыТуровСоревнования.Групповой;
	
	Если ЗначениеЗаполнено(Параметры.НастройкиМестJSON) Тогда
		МассивСтруктур = КоннекторHTTP.JSONВЗначение(Параметры.НастройкиМестJSON);
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(МассивСтруктур, ТаблицаМест);
	Иначе
		Если РежимТура = Перечисления.РежимыТуровСоревнования.Групповой Тогда
			Для Каждого ТекСтр Из ТаблицаГрупп Цикл
				НовСтрока = ТаблицаМест.Добавить();
				НовСтрока.НомерГруппы = ТекСтр.НомерГруппы;
				НовСтрока.МестоС 	= 1;
				НовСтрока.МестоПо	= ТекСтр.Количество;
				НовСтрока.КолМестВСетке = ТекСтр.Количество;
			КонецЦикла;
		Иначе
			НовСтрока = ТаблицаМест.Добавить();
			НовСтрока.МестоС = 1;
			НовСтрока.МестоПо = Параметры.СеткаНа;
			НовСтрока.КолМестВСетке = Параметры.СеткаНа;
			Если ТаблицаГрупп.Количество() > 0 Тогда
				НовСтрока.МестоПо = ТаблицаГрупп[0].Количество;
			КонецЕсли;
		КонецЕсли;			
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаМестМестоСПриИзменении(Элемент)
	
	ТекСтрока = Элементы.ТаблицаМест.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		Если РежимТура <> ПредопределенноеЗначение("Перечисление.РежимыТуровСоревнования.Групповой") Тогда
			Если ТекСтрока.МестоС = 1 Тогда
				ТекСтрока.МестоПо = СеткаНа;	
			Иначе
				ТекСтрока.МестоПо = СеткаНа + ТекСтрока.МестоС -1;	
			КонецЕсли;
		Иначе
			Если ТекСтрока.МестоС = 1 Тогда
				ТекСтрока.МестоПо = ТекСтрока.КолМестВСетке;	
			Иначе
				ТекСтрока.МестоПо = ТекСтрока.КолМестВСетке + ТекСтрока.МестоС -1;	
			КонецЕсли;			
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаМестПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗапомнитьНастройку(Команда)
	
	Если ТаблицаМест.Количество() = 0 Тогда
		Закрыть("");
	Иначе
		Закрыть(ПолучитьТаблицуВJSON());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуВJSON()
	
	Возврат КоннекторHTTP.ЗначениеВJSON(ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаМест.Выгрузить()));
	
КонецФункции

&НаКлиенте
Процедура УдалитьНастройки(Команда)
	ТаблицаМест.Очистить();
	ЗапомнитьНастройку(Команда);
КонецПроцедуры
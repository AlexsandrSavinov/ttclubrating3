
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("СоцСеть", СоцСеть);
	
	Если Параметры.Свойство("ИсточникВызова") Тогда
		
		СсылкаНаДокумент 	= Параметры.ИсточникВызова.СсылкаНаДокумент;
		втТабДок						 = Параметры.ИсточникВызова.ТабДокДляОтправки;
		
		ЗаполнитьЗначенияСвойств(ТабДокДляОтправки,втТабДок,"АвтоМасштаб,ПолеСверху,ПолеСнизу,ПолеСлева,ПолеСправа,РазмерКолонтитулаСверху");
		ТабДокДляОтправки.ВерхнийКолонтитул.Выводить 						= втТабДок.ВерхнийКолонтитул.Выводить;
		ТабДокДляОтправки.ВерхнийКолонтитул.НачальнаяСтраница 	= втТабДок.ВерхнийКолонтитул.НачальнаяСтраница;
		ТабДокДляОтправки.ВерхнийКолонтитул.ТекстСправа 					= втТабДок.ВерхнийКолонтитул.ТекстСправа;
		ТабДокДляОтправки.Вывести(втТабДок);
		
	КонецЕсли; 
	
	Если СоцСеть = Перечисления.СоцСеть.Вконтакте Тогда
		Заголовок = "Отправка во ""Вконтакте""";
		КартинкаСоцСети = БиблиотекаКартинок.ИконкаVK;
		Элементы.Бот.Видимость = Ложь;
		ВыбранноеРасширение = "XLSX";
	ИначеЕсли СоцСеть = Перечисления.СоцСеть.Телеграмм Тогда 
		Заголовок = "Отправка в ""Телеграмм""";
		КартинкаСоцСети = БиблиотекаКартинок.ИконкаТелеграмм;
		АктивныеБоты = РаботаССоциальнымиСетямиСервер.АктивныеБоты();
		Если АктивныеБоты.Количество() > 0 Тогда
			Бот = АктивныеБоты[0].Бот;
		КонецЕсли; 
		ВыбранноеРасширение = "PDF";
		//Элементы.ФормаКомандаОтправитьHTML.Видимость = Ложь; //временно
	КонецЕсли; 
	
	ОбновитьДеревоДляОтправки();
	
	Элементы.ФормаКомандаОтправитьXLSX.Пометка 	= Истина;
	Элементы.ФормаКомандаОтправитьXLSX.ЦветФона 	= Новый Цвет(255, 225, 0);
	//Элементы.ФормаКомандаОтправитьXLSX.Видимость = СоцСеть <> Перечисления.СоцСеть.Телеграмм;
	
	Если Не ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
		ЗаголовокДляДокумента = "Документ от "+ТекущаяДата();
	Иначе
		Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.ПроведениеСоревнования") Тогда
			Если ЗначениеЗаполнено(СсылкаНаДокумент.НазваниеСоревнования) Тогда
				ЗаголовокДляДокумента = СсылкаНаДокумент.НазваниеСоревнования;
			Иначе
				ЗаголовокДляДокумента = Строка(СсылкаНаДокумент);
			КонецЕсли; 
		Иначе
			ЗаголовокДляДокумента = Строка(СсылкаНаДокумент);
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоДляОтправки()
	
	ДеревоДляОтправки.ПолучитьЭлементы().Очистить();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
	Запрос.УстановитьПараметр("СоцСеть", СоцСеть);
	Запрос.Текст = "ВЫБРАТЬ
	|	ПроведениеСоревнованияСписокУчастников.Участник КАК Участник
	|ПОМЕСТИТЬ втВсеУчастники
	|ИЗ
	|	Документ.ПроведениеСоревнования.СписокУчастников КАК ПроведениеСоревнованияСписокУчастников
	|ГДЕ
	|	ПроведениеСоревнованияСписокУчастников.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПроведениеСоревнованияТаблицаБыстрогоРасчета.Участник1
	|ИЗ
	|	Документ.ПроведениеСоревнования.ТаблицаБыстрогоРасчета КАК ПроведениеСоревнованияТаблицаБыстрогоРасчета
	|ГДЕ
	|	ПроведениеСоревнованияТаблицаБыстрогоРасчета.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПроведениеСоревнованияТаблицаБыстрогоРасчета.Участник2
	|ИЗ
	|	Документ.ПроведениеСоревнования.ТаблицаБыстрогоРасчета КАК ПроведениеСоревнованияТаблицаБыстрогоРасчета
	|ГДЕ
	|	ПроведениеСоревнованияТаблицаБыстрогоРасчета.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПубликацияЗаписейВСоцСетях.ТипПубликации КАК ТипПубликации,
	|	ПубликацияЗаписейВСоцСетях.ИД КАК ИД,
	|	ПубликацияЗаписейВСоцСетях.Комментарий КАК Наименование
	|ИЗ
	|	РегистрСведений.ПубликацияЗаписейВСоцСетях КАК ПубликацияЗаписейВСоцСетях
	|ГДЕ
	|	ПубликацияЗаписейВСоцСетях.СоцСеть = &СоцСеть
	|	И ПубликацияЗаписейВСоцСетях.ТипПубликации = ЗНАЧЕНИЕ(Перечисление.ТипПубликации.Группа)
	|	И ПубликацияЗаписейВСоцСетях.Публиковать
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПубликацияЗаписейВСоцСетях.ТипПубликации,
	|	ПубликацияЗаписейВСоцСетях.ИД,
	|	ПубликацияЗаписейВСоцСетях.Участник.Наименование
	|ИЗ
	|	РегистрСведений.ПубликацияЗаписейВСоцСетях КАК ПубликацияЗаписейВСоцСетях
	|ГДЕ
	|	ПубликацияЗаписейВСоцСетях.СоцСеть = &СоцСеть
	|	И ПубликацияЗаписейВСоцСетях.ТипПубликации = ЗНАЧЕНИЕ(Перечисление.ТипПубликации.Пользователь)
	|	И ПубликацияЗаписейВСоцСетях.Публиковать
	|ИТОГИ
	|	ТипПубликации КАК Наименование
	|ПО
	|	ТипПубликации";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		
		ВыборкаИД = Выборка.Выбрать();
		
		КореньСтроки = ДеревоДляОтправки.ПолучитьЭлементы().Добавить();
		КореньСтроки.ЭтоГруппа = Истина;
		КореньСтроки.ТипПубликации = Выборка.ТипПубликации;
		КореньСтроки.Наименование = ?(ЗначениеЗаполнено(Выборка.Наименование), Выборка.Наименование, "Группа");
		
		Пока ВыборкаИД.Следующий() Цикл
			
			НовСтрока = КореньСтроки.ПолучитьЭлементы().Добавить();
			Если Не ЗначениеЗаполнено(ВыборкаИД.Наименование) Тогда
				НовСтрока.ТипПубликации = Строка(ВыборкаИД.ТипПубликации) + " ID: "+ВыборкаИД.ИД;
			Иначе
				НовСтрока.ТипПубликации = ВыборкаИД.Наименование;
			КонецЕсли; 
			НовСтрока.ИД  = ВыборкаИД.ИД;
			НовСтрока.Пометка = Истина;
			
		КонецЦикла; 
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтправитьОбщая(Команда)
	
	ЦветВажного 			= Новый Цвет(255, 225, 0);
	ЦветФонаКнопки 	= Новый Цвет(255, 255, 255);
	
	Если Команда.Имя = "ФормаКомандаОтправитьXLSX" Или Команда.Имя = "КомандаОтправитьXLSX" Тогда
		Элементы.ФормаКомандаОтправитьXLSX.Пометка 	= Истина;
		Элементы.ФормаКомандаОтправитьXLSX.ЦветФона 	= ЦветВажного;
		
		Элементы.ФормаКомандаОтправитьPDF.Пометка 		= Ложь;
		Элементы.ФормаКомандаОтправитьPDF.ЦветФона 	= ЦветФонаКнопки;
		Элементы.ФормаКомандаОтправитьHTML.Пометка 	= Ложь;
		Элементы.ФормаКомандаОтправитьHTML.ЦветФона = ЦветФонаКнопки;
		ВыбранноеРасширение = "XLSX";
	ИначеЕсли Команда.Имя = "ФормаКомандаОтправитьPDF" Или Команда.Имя = "КомандаОтправитьPDF" Тогда 
		Элементы.ФормаКомандаОтправитьPDF.Пометка 		= Истина;
		Элементы.ФормаКомандаОтправитьPDF.ЦветФона 	= ЦветВажного;
		
		Элементы.ФормаКомандаОтправитьHTML.Пометка 	= Ложь;
		Элементы.ФормаКомандаОтправитьHTML.ЦветФона 	= ЦветФонаКнопки;
		Элементы.ФормаКомандаОтправитьXLSX.Пометка 	= Ложь;
		Элементы.ФормаКомандаОтправитьXLSX.ЦветФона 	= ЦветФонаКнопки;
		ВыбранноеРасширение = "PDF";
	ИначеЕсли Команда.Имя = "ФормаКомандаОтправитьHTML" Или Команда.Имя = "КомандаОтправитьHTML" Тогда 
		Элементы.ФормаКомандаОтправитьHTML.Пометка 	= Истина;
		Элементы.ФормаКомандаОтправитьHTML.ЦветФона 	= ЦветВажного;
		
		Элементы.ФормаКомандаОтправитьPDF.Пометка 		= Ложь;
		Элементы.ФормаКомандаОтправитьPDF.ЦветФона 	= ЦветФонаКнопки;
		Элементы.ФормаКомандаОтправитьXLSX.Пометка 	= Ложь;
		Элементы.ФормаКомандаОтправитьXLSX.ЦветФона 	= ЦветФонаКнопки;
		ВыбранноеРасширение = "HTML";
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтправить(Команда)
	
	РаботаССоциальнымиСетямиСервер.СохранитьВариантРазмещенияВВК(ВыбранноеРасширение);
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ТабДокДляОтправки", ТабДокДляОтправки);
	ПараметрыЗакрытия.Вставить("СоцСеть", СоцСеть);
	ПараметрыЗакрытия.Вставить("Формат", ВыбранноеРасширение);
	ПараметрыЗакрытия.Вставить("СсылкаНаДокумент", СсылкаНаДокумент);
	ПараметрыЗакрытия.Вставить("Заголовок", ЗаголовокДляДокумента);
	
	МассивИДДляОтправки = Новый Массив;
	
	Для каждого СтрокаДерева Из ДеревоДляОтправки.ПолучитьЭлементы() Цикл
		Для каждого ПодчиненнаяСтрока Из СтрокаДерева.ПолучитьЭлементы() Цикл
			Если ПодчиненнаяСтрока.Пометка Тогда
				
				СтруктураСтроки = Новый Структура;
				СтруктураСтроки.Вставить("ТипПубликации", СтрокаДерева.ТипПубликации);
				СтруктураСтроки.Вставить("ИД", ПодчиненнаяСтрока.ИД);
				
				МассивИДДляОтправки.Добавить(СтруктураСтроки);
				
			КонецЕсли; 
		КонецЦикла; 
	КонецЦикла; 
	
	ПараметрыЗакрытия.Вставить("ИДДляОтправки", МассивИДДляОтправки);
	
	ДанныеПоТокенам = ПолучитьДанныеДляОтправкиНаСервере(Бот);
	
	ПараметрыЗакрытия.Вставить("ТокенВК" , "");
	ПараметрыЗакрытия.Вставить("Бот", "");
	ПараметрыЗакрытия.Вставить("ТокенБота", "");
	
	ЗаполнитьЗначенияСвойств(ПараметрыЗакрытия, ДанныеПоТокенам);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеДляОтправкиНаСервере(Бот)
	
	ПараметрыВозврата = Новый Структура;
	ПараметрыВозврата.Вставить("ТокенВК", РаботаССоциальнымиСетямиСервер.ПолучитьТокенВК());
	
	Если Константы.ИспользоватьБотаTtclubrating.Получить() Тогда
		ПараметрыВозврата.Вставить("Бот", Неопределено);
		ПараметрыВозврата.Вставить("ТокенБота", РаботаССоциальнымиСетямиКлиентСервер.ПолучитьТокенБотаТТ().Токен);
		Если ПараметрыВозврата.ТокенБота = "" Тогда
			ПараметрыВозврата.ТокенБота 	= Серверные.ЗначениеРеквизитаИзСсылки(Бот, "Токен");
			ПараметрыВозврата.Бот				= Бот;
		КонецЕсли; 
	Иначе
		ПараметрыВозврата.Вставить("Бот", Бот);
		ПараметрыВозврата.Вставить("ТокенБота", Серверные.ЗначениеРеквизитаИзСсылки(Бот, "Токен"));
	КонецЕсли; 
	
	Возврат ПараметрыВозврата;
	
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПоследнийВариант = РаботаССоциальнымиСетямиСервер.ПолучитьПоследнийВариантРазмещенияВВК();
	Если ПоследнийВариант <> Неопределено И ЗначениеЗаполнено(ПоследнийВариант) Тогда
		Если ПоследнийВариант = "XLSX" Тогда
			КомандаОтправитьОбщая(Элементы.ФормаКомандаОтправитьXLSX);	
		ИначеЕсли ПоследнийВариант = "PDF" Тогда 
			КомандаОтправитьОбщая(Элементы.ФормаКомандаОтправитьPDF);	
		ИначеЕсли ПоследнийВариант = "HTML" Тогда 
			КомандаОтправитьОбщая(Элементы.ФормаКомандаОтправитьHTML);	
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры
 
 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ТекущийРазряд = "Присвоить разряд";
		Элементы.КонтактнаяИнформация.Видимость = Ложь;
		Элементы.Группа4.Видимость = Ложь;
		Элементы.СсылкаНаКартинку.Видимость = Ложь;
	Иначе
		//ТекущийРейтинг = СерверныеСервер.ПолучитьТекущийРейтингУчастника(Объект.Ссылка,ТекущаяДата());
		ТекущийРазряд = СерверныеСервер.ПолучитьРазрядЗваниеУчастника(Объект.Ссылка,ТекущаяДата());
		СсылкаНаКартинку = ПолучитьНавигационнуюСсылку(Объект.Ссылка,"ФайлКартинки");
		Если Не Объект.СкрытьДопИнфо Тогда
			Элементы.КонтактнаяИнформация.Видимость = Истина;
			Элементы.Группа4.Видимость = Истина;
			Элементы.СсылкаНаКартинку.Видимость = Истина;
			Элементы.Дополнительно.Заголовок = "Скрыть дополнительную информацию";
		Иначе	
			Элементы.КонтактнаяИнформация.Видимость = Ложь;
			Элементы.Группа4.Видимость = Ложь;
			Элементы.СсылкаНаКартинку.Видимость = Ложь;
			Элементы.Дополнительно.Заголовок = "Раскрыть полную информацию";
		КонецЕсли; 
	КонецЕсли;
	ТаблицаВидовРейтинга.Параметры.УстановитьЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Участник"),Объект.Ссылка);
	ТекущийРейтинг = "Скорректировать рейтинг";
	Если ТекущийРазряд <> "Присвоить разряд" Тогда
		Элементы.ТекущийРазряд.Гиперссылка = Ложь;
	КонецЕсли;
	ПосчитатьКоличествоЛет();
	Если Объект.Тренеры.Количество() > 0 Тогда
		Элементы.Тренер.Видимость = Ложь;
		Элементы.Тренеры.Видимость = Истина;
		Списком = Истина;		
	КонецЕсли; 
КонецПроцедуры

&НаСервере
Процедура ПосчитатьКоличествоЛет()
	
	Если Объект.ДатаРождения <> Дата('00010101') Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
					   |	РАЗНОСТЬДАТ(&Дата1, &Дата2, ГОД) КАК КолЛет";
		Запрос.УстановитьПараметр("Дата1",Объект.ДатаРождения);
		Запрос.УстановитьПараметр("Дата2",ТекущаяДата());
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		КолДнейСоДняРождения = Выборка.КолЛет;
		
		Элементы.КолЛет.Заголовок = ""+КолДнейСоДняРождения+" л.";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	ФИО = Объект.Наименование;
	Фамилия 	 = ОбщийКлиентСервер.ВыделитьСлово(ФИО);
	Имя     		 = ОбщийКлиентСервер.ВыделитьСлово(ФИО);
	Отчество 	 = ОбщийКлиентСервер.ВыделитьСлово(ФИО);
	
	Если Не ЗначениеЗаполнено(Объект.Пол) Тогда
		//попробуем предположить какой пол
		ПоследняяБуква = Прав(Фамилия,1);
		Если ПоследняяБуква = "а" Или ПоследняяБуква = "А" Тогда
			Объект.Пол = ПредопределенноеЗначение("Перечисление.ПолУчастника.Женский");
		Иначе
			Объект.Пол = ПредопределенноеЗначение("Перечисление.ПолУчастника.Мужской");
		КонецЕсли;
		ЭтотОбъект.Модифицированность = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.ИдентификаторНазначения = ЭтотОбъект.УникальныйИдентификатор;
		Сообщение.Текст = "Заполнен пол участника";
		//Сообщение.Поле = "Объект.Пол";
		Сообщение.Сообщить(); 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если Объект.Пол = ПредопределенноеЗначение("Перечисление.ПолУчастника.ПустаяСсылка") Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Поле = "Объект.Пол";
		Сообщение.УстановитьДанные(Объект);
		Сообщение.Текст = "Заполните пол участника";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	Если Не Списком И Объект.Тренеры.Количество() > 0 Тогда
		Объект.Тренеры.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НаименованиеПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийРазрядНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ФормаДока = ПолучитьФорму("Документ.ПрисвоитьРазряд.ФормаОбъекта");
	НовСтрока = ФормаДока.Объект.Участники.Добавить();
	НовСтрока.Участник = Объект.Ссылка;
	ФормаДока.Открыть();
КонецПроцедуры
 
&НаКлиенте
Процедура СсылкаНаКартинкуНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	АдресКартинки = "";
	Диалог=Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.МножественныйВыбор=Ложь;
	Диалог.Фильтр=  "Все файлы |*.*|Формат JPEG(*.jpeg;*.jpg)|*.jpeg;*.jpg |Формат PNG(*.png*)|*.png";
	Диалог.Показать(Новый ОписаниеОповещения("СсылкаНаКартинкуНажатиеЗавершение1", ЭтотОбъект, Новый Структура("АдресКартинки, Диалог", АдресКартинки, Диалог)));
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаКартинкуНажатиеЗавершение1(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	АдресКартинки = ДополнительныеПараметры.АдресКартинки;
	Диалог = ДополнительныеПараметры.Диалог;

	Если (ВыбранныеФайлы <> Неопределено) Тогда
		НачатьПомещениеФайлаНаСервер(Новый ОписаниеОповещения("СсылкаНаКартинкуНажатиеЗавершение", ЭтотОбъект,
			Новый Структура("АдресКартинки", АдресКартинки)), АдресКартинки, Диалог.ПолноеИмяФайла, Ложь,
			Новый УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаКартинкуНажатиеЗавершение(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
		
	СсылкаНаКартинку = Адрес;
	Модифицированность = Истина;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(СсылкаНаКартинку) Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(СсылкаНаКартинку);
		ТекущийОбъект.ФайлКартинки = Новый ХранилищеЗначения(ДвоичныеДанные,Новый СжатиеДанных(9));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(СсылкаНаКартинку) Тогда 
		УдалитьИзВременногоХранилища(СсылкаНаКартинку);
		СсылкаНаКартинку = ПолучитьНавигационнуюСсылку(ТекущийОбъект.Ссылка,"ФайлКартинки");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийРейтингНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ФормаДока = ПолучитьФорму("Документ.ВводНачальногоРейтинга.ФормаОбъекта");
	НовСтрока = ФормаДока.Объект.Данные.Добавить();
	НовСтрока.Игрок = Объект.Ссылка;
	ФормаДока.Открыть();
КонецПроцедуры

&НаКлиенте
Процедура ДополнительноНажатие(Элемент)
	
	Если Элементы.КонтактнаяИнформация.Видимость Тогда
		//скрываем
		Элементы.КонтактнаяИнформация.Видимость = Ложь;
		Элементы.Группа4.Видимость = Ложь;
		Элементы.СсылкаНаКартинку.Видимость = Ложь;
		Объект.СкрытьДопИнфо = Истина;
		Элементы.Дополнительно.Заголовок = "Раскрыть полную информацию";
	Иначе
		Элементы.КонтактнаяИнформация.Видимость = Истина;
		Элементы.Группа4.Видимость = Истина;
		Элементы.СсылкаНаКартинку.Видимость = Истина;
		Объект.СкрытьДопИнфо = Ложь;
		Элементы.Дополнительно.Заголовок = "Скрыть дополнительную информацию";
	КонецЕсли; 
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаРожденияПриИзменении(Элемент)
	ПосчитатьКоличествоЛет();
КонецПроцедуры

&НаКлиенте
Процедура ТренерПриИзменении(Элемент)
	ЗаполнитьДанныеИзТренера();	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеИзТренера()
	Если Не ЗначениеЗаполнено(Объект.Клуб) Тогда
		Объект.Клуб 	= Объект.Тренер.Клуб;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Город) Тогда
		Объект.Город 	= Объект.Тренер.Город;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СпискомПриИзменении(Элемент)
	
	Если Списком Тогда
		Элементы.Тренер.Видимость = Ложь;
		Если Объект.Тренеры.Количество() = 0 Тогда
			НовСтр = Объект.Тренеры.Добавить();
			НовСтр.Тренер = Объект.Тренер;
		КонецЕсли; 
		Элементы.Тренеры.Видимость = Истина;
	Иначе
		Элементы.Тренер.Видимость = Истина;
		Элементы.Тренеры.Видимость = Ложь;
	КонецЕсли; 
	ЭтотОбъект.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТренерыПриАктивизацииСтроки(Элемент)
	
	ТекСтрока = Элементы.Тренеры.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		Если ТекСтрока.Тренер = Объект.Тренер Тогда
			Элементы.ТренерыКомандаОсновной.Пометка = Истина;
		Иначе
			Элементы.ТренерыКомандаОсновной.Пометка = Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОсновной(Команда)
	
	ТекСтрока = Элементы.Тренеры.ТекущиеДанные;
	Если ТекСтрока <> Неопределено Тогда
		Если Элементы.ТренерыКомандаОсновной.Пометка Тогда
			Элементы.ТренерыКомандаОсновной.Пометка = Ложь;
			Объект.Тренер = Неопределено;
		Иначе
			Элементы.ТренерыКомандаОсновной.Пометка = Истина;
			Объект.Тренер = ТекСтрока.Тренер;
		КонецЕсли; 		
	КонецЕсли;
	
КонецПроцедуры
 

	



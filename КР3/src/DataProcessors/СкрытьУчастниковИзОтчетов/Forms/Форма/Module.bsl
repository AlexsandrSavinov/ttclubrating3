
&НаКлиенте
Процедура КомандаЗаполнить(Команда)
	КомандаЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура КомандаЗаполнитьНаСервере()
	
	ТаблицаУчастников.Очистить();
	
	СКД = Обработки.СкрытьУчастниковИзОтчетов.ПолучитьМакет("СхемаПодбора");
	НастройкиКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД,НастройкиКомпоновки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ТЗ = Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(ТЗ);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
		
	Для Каждого ТекСтр Из ТЗ Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаУчастников.Добавить(),ТекСтр);
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	Для Каждого ТекСтр Из ТаблицаУчастников Цикл
		ТекСтр.Пометка = Истина;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	Для Каждого ТекСтр Из ТаблицаУчастников Цикл
		ТекСтр.Пометка = Ложь;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура Скорректировать(Команда)
	СкорректироватьНаСервере();
	ПоказатьОповещениеПользователя("Корректировка",,"Отмеченные участники теперь не будут выводиться в отчетах");
КонецПроцедуры

&НаСервере
Процедура СкорректироватьНаСервере()
	
	Для Каждого ТекСтр Из ТаблицаУчастников Цикл
		Если ТекСтр.Пометка Тогда
			ОбъектУчастник = ТекСтр.Участник.ПолучитьОбъект();
			ОбъектУчастник.НеВыводитьВОтчетах = Истина;
			ОбъектУчастник.Записать();
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СКД = Обработки.СкрытьУчастниковИзОтчетов.ПолучитьМакет("СхемаПодбора");
	АдресВТ = ПоместитьВоВременноеХранилище(СКД,УникальныйИдентификатор);
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресВТ);
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	
КонецПроцедуры

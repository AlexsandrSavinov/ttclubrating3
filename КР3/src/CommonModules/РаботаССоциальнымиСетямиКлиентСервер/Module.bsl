
Функция УстановитьСоединениеСТелеграмм(ИнтернетПрокси = Неопределено, Таймаут = 0) Экспорт
	
	Если ИнтернетПрокси <> Неопределено Тогда
		Возврат Новый HTTPСоединение("api.telegram.org",443,,,ИнтернетПрокси, Таймаут, Новый ЗащищенноеСоединениеOpenSSL()); 
	Иначе
		СписокПроксиСерверов = РаботаССоциальнымиСетямиСервер.ПолучитьВсеПроксиСервера();
		мИнтернетПрокси = Неопределено;
		Если СписокПроксиСерверов.Количество() > 0 Тогда //проверяем по прокси
			//пока берем первый
			ДанныеДляПрокси = СписокПроксиСерверов[0];
			мИнтернетПрокси = СоздатьИнтернетПрокси(ДанныеДляПрокси);
		КонецЕсли; 
		Если мИнтернетПрокси = Неопределено Тогда
			Возврат Новый HTTPСоединение("api.telegram.org",443,,,,,Новый ЗащищенноеСоединениеOpenSSL()); 
		Иначе
			Возврат Новый HTTPСоединение("api.telegram.org",443,,,мИнтернетПрокси, Таймаут, Новый ЗащищенноеСоединениеOpenSSL()); 
		КонецЕсли; 		
	КонецЕсли; 
	
КонецФункции 

Функция СоздатьИнтернетПрокси(ПараметрыПрокси) Экспорт
	
	мИнтернетПрокси = Новый ИнтернетПрокси(Ложь);
	ПользовательДляУстановки 	= ?(ЗначениеЗаполнено(ПараметрыПрокси.Пользователь),ПараметрыПрокси.Пользователь,"");
	ПарольДляУстановки				= ?(ЗначениеЗаполнено(ПараметрыПрокси.Пароль),ПараметрыПрокси.Пароль,"");
	мИнтернетПрокси.Установить(ПараметрыПрокси.Протокол, ПараметрыПрокси.ИПАдрес, ПараметрыПрокси.Порт, ПользовательДляУстановки, ПарольДляУстановки, Ложь);

	Возврат мИнтернетПрокси;
	
КонецФункции

Функция ПроверитьДоступностьТелеграмм(ТокенТелеграмм = "", Соединение = Неопределено) Экспорт
	
	Если ТокенТелеграмм = "" Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не установлен токен для проверки.";
		Сообщение.Сообщить(); 
		Возврат "";
	КонецЕсли; 
	
	Если Соединение = Неопределено Тогда
		Соединение = УстановитьСоединениеСТелеграмм();
	КонецЕсли; 
	
	Ресурс = "bot" + ТокенТелеграмм + "/getMe"; 
	Запрос = Новый HTTPЗапрос(Ресурс); 
	Запрос.Заголовки.Вставить("Content-type","application/json");
	Ответ = Соединение.Получить(Запрос); 
	
	Если Ответ.КодСостояния = 200 Тогда
		СтруктураОтвета = ОбщийКлиентСервер.СформироватьОтветHTTPВСтруктуру(Ответ);
		СтрокаОтвета = "";
		Для каждого ТекСтр Из СтруктураОтвета.result Цикл
			СтрокаОтвета = СтрокаОтвета + ТекСтр.Ключ + ": "+ТекСтр.Значение + Символы.ПС
		КонецЦикла;  
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "УСПЕШНО" + Символы.ПС + СтрокаОтвета;
		Сообщение.Сообщить(); 
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "ОШИБКА" + Символы.ПС + Ответ.ПолучитьТелоКакСтроку();
		Сообщение.Сообщить(); 	
	КонецЕсли; 
	
КонецФункции

Функция ВыполнитьКомандуТелеграммБота(ДанныеБота, Соединение = Неопределено, Команда = "getMe") Экспорт
	
	//https://api.telegram.org/bot<Токен>/getUpdates
	
	Если Соединение = Неопределено Тогда
		Соединение = РаботаССоциальнымиСетямиКлиентСервер.УстановитьСоединениеСТелеграмм();
	КонецЕсли; 
	
	Адрес       = СтрШаблон("/bot%1/%2", ДанныеБота.Токен, Команда);
	Заголовки   = Новый Соответствие;
	Запрос      = Новый HTTPЗапрос(Адрес, Заголовки);
	// GET
	Ответ       = Соединение.Получить(Запрос);
	
	СтруктураОтвета    = ОбщийКлиентСервер.СформироватьОтветHTTPВСтруктуру(Ответ);
	СтруктураОтвета.Вставить("КодСостояния", Ответ.КодСостояния);
	
	Возврат СтруктураОтвета;
	
КонецФункции

Функция ПолучитьТокенБотаТТ() Экспорт
	
	ДанныеВозврата = Новый Структура;
	Попытка
		ЗапросHTTP = Новый HTTPЗапрос;
		ЗапросHTTP.АдресРесурса = "updates/bot_tg.txt";
		Соединение = Новый HTTPСоединение("www.ttclubrating.ru");
		ОтветHTTP = Соединение.Получить(ЗапросHTTP);
		Если ОтветHTTP.КодСостояния = 200 Тогда
			Стр = СокрЛП(ОтветHTTP.ПолучитьТелоКакСтроку());
		Иначе
			Стр = "";
		КонецЕсли; 
		ДанныеВозврата.Вставить("Токен", Стр);
	Исключение
		ДанныеВозврата.Вставить("Токен", "");
	КонецПопытки;
	
	Возврат ДанныеВозврата;
	
КонецФункции

 
